plugins {
    id 'java'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'com.github.dkorotych.gradle-maven-exec' version '2.2.1'
    id 'maven-publish'
    id 'signing'
}

group 'eu.cyber-geiger'
version '0.0.1b-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url = uri('https://repo.maven.apache.org/maven2')
        mavenContent {
            releasesOnly()
        }
    }
    maven {
        url = uri('https://oss.sonatype.org/content/repositories/snapshots/')
        mavenContent {
            snapshotsOnly()
        }
    }
}

configurations {
    checkstyleConfig
}

dependencies {
    compile group: 'com.puppycrawl.tools', name: 'checkstyle', version: '10.0'
    checkstyleConfig('com.puppycrawl.tools:checkstyle:10.0') { transitive = false }

    // junit
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.11.0'
    testImplementation('junit:junit:4.13.2')
    testRuntimeOnly('org.junit.vintage:junit-vintage-engine:5.8.2')
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.12.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.12.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-jul', version: '2.12.1'

}
// no caching for modules
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy {
        force 'xml-apis:xml-apis:1.0.b2'
    }
}

group = 'eu.cyber-geiger'
version = '0.0.2'
description = 'Client-only implementation of the geiger toolbox.'
sourceCompatibility = '1.8'

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
        )
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

jar.dependsOn shadowJar
jar.dependsOn test

check.dependsOn checkstyleMain

/* integrate junit */
test {
    systemProperty 'java.util.logging.manager', 'org.apache.logging.log4j.jul.LogManager'
    useJUnitPlatform {}
    maxHeapSize = '1G'
}

checkstyle {
    toolVersion '8.38'
    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
    ignoreFailures = false
    maxWarnings = 0
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}


java {
    withSourcesJar()
    withJavadocJar()
}

artifacts {
    archives javadocJar, sourcesJar
}

static def gitBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

publishing {
    repositories {
        maven {
            name = "sonatype"
            url = (gitBranch() == "main") ?
                    'https://oss.sonatype.org/service/local/staging/deploy/maven2/' :
                    'https://oss.sonatype.org/content/repositories/snapshots/'

            credentials {
                username findProperty('ossrhUsername') as String
                password findProperty('ossrhPassword') as String
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.name
            from components.java

            version = gitBranch() == "main" ? version : version + '-SNAPSHOT'

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'toolbox-communicationApi'
                description = 'The communication API required for interlinking independent plugins'
                url = 'https://github.com/cyber-geiger/toolbox-communicationAPI'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'mgwerder'
                        name = 'Martin Gwerder'
                        email = 'martin.gwerder@fhnw.ch'
                    }
                    developer {
                        id = 'sleemann'
                        name = 'Sacha Leemann'
                        email = 'sacha.leemann@fhnw.ch'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/cyber-geiger/toolbox-communicationAPI.git'
                    developerConnection = 'scm:git:ssh://github.com/cyber-geiger/toolbox-communicationAPI.git'
                    url = 'http://github.com/cyber-geiger/toolbox-communicationAPI'
                }
            }

        }

    }

}

signing {
    sign publishing.publications.mavenJava
}

